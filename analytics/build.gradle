plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'kotlin-parcelize'
}

version = "2.18.3"

android {
    namespace 'com.bluetriangle.analytics'
    compileSdk 31

    defaultConfig {
        minSdk 16
        targetSdk 31

        buildConfigField "String", "SDK_VERSION", "\"$version\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Encapsulates options for running tests.
    testOptions {
        reportDir "$rootDir/test-reports"
        resultsDir "$rootDir/test-results"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.7"
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.8.21'

    api("androidx.compose.foundation:foundation:1.4.3")
    implementation 'androidx.lifecycle:lifecycle-process:2.6.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // Mockito framework
    testImplementation "org.mockito:mockito-core:5.13.0"
    // mockito-kotlin
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.4.0"
    testImplementation 'org.robolectric:robolectric:4.10'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'
    testImplementation 'com.squareup.okhttp3:okhttp-tls:4.9.1'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.blue-triangle-tech'
                artifactId = 'btt-android-sdk'
                version = "2.18.3"
            }
        }
    }
}

tasks.withType(Test).configureEach {
    reports {
        junitXml.getRequired().set(true)  // Enable XML test results
        html.getRequired().set(true)      // Enable HTML test report
        junitXml.getOutputLocation().set(file("$rootDir/test-results"))
        html.getOutputLocation().set(file("$rootDir/test-reports"))
    }
}